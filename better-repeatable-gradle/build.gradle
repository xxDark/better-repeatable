plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    shadeOnly project(':better-repeatable-api')
    shadeOnly project(':better-repeatable-javac')
}

jar {
    from { configurations.shadeOnly.collect { zipTree(it) } }
}

gradlePlugin {
    website.set('https://github.com/xxDark/better-repeatable')
    vcsUrl.set('https://github.com/xxDark/better-repeatable')
    plugins {
        betterRepeatable {
            id = 'dev.xdark.betterrepeatable'
            implementationClass = 'dev.xdark.betterrepeatable.GradlePlugin'
            displayName = 'BetterRepeatable Plugin'
            description = 'Integration for compiler changes made by better-repeatable'
            tags.set(['annotations', 'javac'])
        }
    }
}
